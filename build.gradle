description = 'Gradle wrapper for OpenSSL build'

apply plugin: 'base'
apply plugin: 'ivy-publish'

buildscript {
    ext.svtFunctionsVersion = '6.0.+'
    ext.osPackageVersion = '4.4.0'

    repositories {
        mavenLocal()
        maven {
            name "ArtifactoryRepo"
            // see gradle.properties
            url artifactoryUrl
        }
    }

    dependencies {
        classpath "com.simplivity.gradle.plugins:gradle-svtfunctions-plugin:$svtFunctionsVersion"
        classpath "com.netflix.nebula:gradle-ospackage-plugin:$osPackageVersion"
    }
}

group = "openssl"

apply plugin: 'com.simplivity.svt-functions'

import groovyx.net.http.ContentType
import org.jfrog.artifactory.client.*
import org.jfrog.artifactory.client.impl.*
import org.jfrog.artifactory.client.model.impl.FileImpl

def publishToArtifactoryDebianRepo(File debFile, String repo) {
    Artifactory artifactory = ArtifactoryClient.create(artifactoryContextUrl, artifactory_user, artifactory_password)

    def hashDir = debFile.name.substring(0,1)
    def uploadPath = "pool/${hashDir}/svt-openssl-fips/${debFile.name};deb.distribution=svt-trusty;deb.component=omnistack;deb.architecture=amd64"

    def uploaded = artifactory.repository(repo).upload(uploadPath, debFile).bySha1Checksum().doUpload()
    println uploaded.downloadUri
}

// Lifecycle task is a no-op on Windows, needs to exist for Matrix builds.
def publishDeb = tasks.create(name: 'publishDeb', group: 'Build') {
    description = 'Publish the Debian Archives to Artifactory'
    enabled = currentOs.linux
}

if (currentOs.linux) {
    apply plugin: "nebula.ospackage-base"

    ext.buildPlatform = 'linux_x64'

    // Linux builds generate both static and shared libraries in one execution
    [ 'Debug', 'Release' ].each { mode ->
        tasks.create(name: "makeLinux${mode}", type: Exec, group: 'Build') {
            description = "Generates the Linux ${mode} configuration"
            outputs.dir file("$buildDir/${mode}")

            environment BUILD_DIR: buildDir
            executable 'util/buildFOM.sh'
            args mode
        }

        [ 'Shared', 'Static' ].each { type ->
            tasks.create(name: "assemble${type}${mode}Nar", type: Zip, group: 'Build') {
                description = "Assembles a zip archive containing the OpenSSL Linux ${type} ${mode} library."
                dependsOn "makeLinux${mode}"
                project.assemble.dependsOn it

                baseName = 'openssl'
                classifier = "linux_x64-${type.toLowerCase()}-${mode.toLowerCase()}"
                extension = 'nar'
                destinationDir = file("$buildDir/distributions")

                into('include') {
                    from "$buildDir/${mode}/openssl-install/usr/include"
                    from ("$buildDir/${mode}/fips-install/include") {
                        exclude '**/ossl_typ.h'
                    }
                    fileMode 0644
                }
                into('bin') {
                    from "$buildDir/${mode}/openssl-install/usr/bin"
                    fileMode 0755
                }
                into('lib') {
                    from("$buildDir/${mode}/openssl-install/usr/lib/x86_64-linux-gnu") {
                        if (type == 'Static') {
                            include 'lib*.a'
                        } else {
                            include 'lib*.so', 'lib*.so.*'
                        }
                    }
                    if (type == 'Static') {
                        from("$buildDir/${mode}/fips-install/lib") {
                            include 'fipscanister.*'
                        }
                    }
                    fileMode 0755
                }
            }
        }

        def buildDeb = tasks.create(name: "buildDeb${mode}", type: Deb, group: 'Build') {
            description = "Assembles a Debian archive containing the OpenSSL Linux ${mode} library."
            dependsOn "makeLinux${mode}"
            project.assemble.dependsOn it

            packageName = (mode == 'Debug') ? 'svt-openssl-fips-dbg' : 'svt-openssl-fips'

            description 'SVT OpenSSL FIPS Debian package ($mode)'
            distribution 'svt-trusty'
            arch 'amd64'
            priority 'required'

            ext.changesPath = file(archivePath.absolutePath.replaceAll('\\.deb$', '.changes'))

            includeEmptyDirs = false
            user 'root'

            def installBaseDir = '/usr/share/svt-openssl-fips'
            into("$installBaseDir/include") {
                from "$buildDir/${mode}/openssl-install/usr/include"
                from "$buildDir/${mode}/fips-install/include"
                fileMode 0644
            }
            into("$installBaseDir/bin") {
                from "$buildDir/${mode}/openssl-install/usr/bin"
                fileMode 0755
            }
            into("$installBaseDir/lib") {
                from "$buildDir/${mode}/openssl-install/usr/lib"
            }
        }

        tasks.create(name: "publishDeb${mode}", group: 'Build') {
            description = "Publishes the Debian archive for OpenSSL Linux ${mode} library to Artifactory"
            dependsOn buildDeb
            publishDeb.dependsOn it

            doLast {
                publishToArtifactoryDebianRepo(buildDeb.archivePath, debianRepo)
                publishToArtifactoryDebianRepo(buildDeb.changesPath, debianRepo)
            }
        }

        // OpenSSL Engines are created separately in the lib/engines directory and
        // only for the shared library mode. Package them separately.
        tasks.create(name: "assemble${mode}Engines", type: Zip, group: 'Build') {
            description = "Assembles a zip archive of the FIPS crypto engines for ${mode} builds."
            dependsOn "makeLinux${mode}"
            project.assemble.dependsOn it

            baseName = "openssl"
            classifier = "${buildPlatform}-engines-${mode.toLowerCase()}"
            extension = 'zip'
            destinationDir = file("$buildDir/distributions")

            into('lib') {
                from("$buildDir/${mode}/openssl-install/usr/lib/x86_64-linux-gnu") {
                    include 'engines/*.so'
                }
                fileMode 0755
            }
        }

        // We only need the Static flavor of these tools to properly process the FIPS builds.
        tasks.create(name: "assemble${mode}FipsTools", type: Zip, group: 'Build') {
            description = "Assembles a zip archive of the FIPS tools and sources for ${mode} builds."
            dependsOn "makeLinux${mode}"
            project.assemble.dependsOn it

            baseName = "openssl"
            classifier = "${buildPlatform}-fips-tools-${mode.toLowerCase()}"
            extension = 'zip'
            destinationDir = file("$buildDir/distributions")

            into('bin') {
                from "$buildDir/${mode}/fips-install/bin"
                fileMode 0755
            }
            into('src') {
                from("$buildDir/${mode}/fips-install/lib") {
                    include 'fips_premain.*'
                }
                fileMode 0644
            }
        }
    }
} else {
    ext.buildPlatform = 'windows_x64'

    // Build openssl with VS 2010, set to 'msvc-14.1' to build with VS 2017
    ext.toolset       = 'msvc-10.0'
    [ 'Debug', 'Release' ].each { mode ->
        [ 'Shared', 'Static' ].each { type ->
            tasks.create(name: "makeWindows${type}${mode}", type: Exec, group: 'Build') {
                description = "Generates the Windows ${type} ${mode} configuration"
                outputs.dir file("$buildDir/${type}/${mode}")

                def vcvarsall
                switch (toolset) {
                    case 'msvc-10.0':
                        vcvarsall = 'C:\\Program Files (x86)\\Microsoft Visual Studio 10.0\\VC\\vcvarsall.bat'
                        break;
                    case 'msvc-14.1':
                        // VS 2017 provides a executable vswhere.exe which will provide location info about
                        // where VS 2017 is installed; we will use this to try to get the path to vcvarsall.bat
                        def vswhereCmd = 'c:\\Program Files (x86)\\Microsoft Visual Studio\\Installer\\vswhere.exe'
                        def vswhereOut = vswhereCmd.execute().text
                        def vswhereProperty = "installationPath"
                        def installationPath = vswhereOut.readLines().findResult { line ->
                            (line =~ /^${vswhereProperty}: (.+)$/).with { m ->
                                if( m.matches() ) {
                                    m[ 0 ][ 1 ] as String
                                }
                            }
                        }
                        if (! installationPath) {
                            throw new GradleException("ERROR: vcvarsall.bat script not found.\n" +
                                                      "vswhere.exe output:\n${vswhereOut}")
                        }
                        vcvarsall = "${installationPath}\\VC\\Auxiliary\\Build\\vcvarsall.bat"
                        break
                    default:
                        throw new GradleException('toolset is not valid')
                }
                workingDir projectDir
                environment BUILD_DIR: buildDir
                executable 'util/buildFOM.bat'
                args type, mode, vcvarsall
            }

            tasks.create(name: "assemble${type}${mode}Nar", type: Zip, group: "Build") {
                description = "Assembles a zip archive containing the OpenSSL Windows ${type} ${mode} library."
                dependsOn "makeWindows${type}${mode}"
                project.assemble.dependsOn it

                baseName = 'openssl'
                classifier = "${buildPlatform}-${type.toLowerCase()}-${mode.toLowerCase()}"
                extension = 'nar'
                destinationDir = file("$buildDir/distributions")

                into('include') {
                    from "$buildDir/${type}/${mode}/openssl-install/include"
                    from "$buildDir/${type}/${mode}/fips-install/include"
                }
                into('bin') {
                    from "$buildDir/${type}/${mode}/openssl-install/bin"
                    include '*.exe'
                }
                into('lib') {
                    from("$buildDir/${type}/${mode}/openssl-install/lib") {
                        include '*.lib'
                    }
                    if (type == 'Static') {
                        from("$buildDir/${type}/${mode}/fips-install/lib") {
                            include 'fipscanister.lib*'
                        }
                    } else {
                        from("$buildDir/${type}/${mode}/openssl-install/bin") {
                            include '*.dll'
                        }
                    }
                }
            }
        }

        // OpenSSL Engines are created separately in the lib/engines directory and
        // only for the shared library mode. Package them separately.
        tasks.create(name: "assemble${mode}Engines", type: Zip, group: 'Build') {
            description = "Assembles a zip archive of the FIPS crypto engines for ${mode} builds."
            dependsOn "makeWindowsShared${mode}"
            project.assemble.dependsOn it

            baseName = "openssl"
            classifier = "${buildPlatform}-engines-${mode.toLowerCase()}"
            extension = 'zip'
            destinationDir = file("$buildDir/distributions")

            into('lib') {
                from("$buildDir/Shared/${mode}/openssl-install/lib") {
                    include 'engines/*.dll'
                }
            }
        }

        // We only need the Static flavor of these tools to properly process the FIPS builds.
        tasks.create(name: "assemble${mode}FipsTools", type: Zip, group: 'Build') {
            description = "Assembles a zip archive of the FIPS tools and sources for ${mode} builds."
            dependsOn "makeWindowsStatic${mode}"
            project.assemble.dependsOn it

            baseName = "openssl"
            classifier = "${buildPlatform}-fips-tools-${mode.toLowerCase()}"
            extension = 'zip'
            destinationDir = file("$buildDir/distributions")

            into('bin') {
                from("$buildDir/Static/${mode}/fips-install/bin") {
                    include '*.exe', '*.pl', 'msincore'
                }
            }
            into('src') {
                from("$buildDir/Static/${mode}/fips-install/lib") {
                    include 'fips_premain.*'
                }
            }
        }
    }
}

publishing {
    publications {
        OpenSSLNar(IvyPublication) {
            module 'openssl'
            configurations {
                "${buildPlatform}-static-debug" {}
                "${buildPlatform}-static-release" {}
                "${buildPlatform}-shared-debug" {}
                "${buildPlatform}-shared-release" {}
                "${buildPlatform}-fips-tools-debug" {}
                "${buildPlatform}-fips-tools-release" {}
                "${buildPlatform}-engines-debug" {}
                "${buildPlatform}-engines-release" {}
                "default" {}
            }
            artifact(assembleStaticDebugNar) {
                conf "${buildPlatform}-static-debug"
            }
            artifact(assembleStaticReleaseNar) {
                conf "${buildPlatform}-static-release"
            }
            artifact(assembleSharedDebugNar) {
                conf "${buildPlatform}-shared-debug"
            }
            artifact(assembleSharedReleaseNar) {
                conf "${buildPlatform}-shared-release"
            }
            artifact(assembleDebugFipsTools) {
                conf "${buildPlatform}-fips-tools-debug"
            }
            artifact(assembleReleaseFipsTools) {
                conf "${buildPlatform}-fips-tools-release"
            }
            artifact(assembleDebugEngines) {
                conf "${buildPlatform}-engines-debug"
            }
            artifact(assembleReleaseEngines) {
                conf "${buildPlatform}-engines-release"
            }
            descriptor.withXml {
                asNode().info[0].appendNode('description', 'openssl library')
            }
        }
    }
}
